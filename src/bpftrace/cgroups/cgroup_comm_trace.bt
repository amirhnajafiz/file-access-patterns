#!/usr/bin/env bpftrace
// file: src/bpftrace/cgroups/debug/cgroup_comm_trace.bt
// usage: sudo bpftrace -o /tmp/tracing.out cgroup_comm_trace.bt <CGROUP_ID> <NAME> <DEBUG>

BEGIN
{
  @tracked_cgid = (uint64)$1;
  @tracked_comm = str($2);
  @debug = (int8)$3;

  printf("[%s] START tracing file-access events for CGROUP ID %llu on %s\n",
    strftime("%Y-%m-%d %H:%M:%S", nsecs), $1, str($2));
  if (@debug == 0) {
    print("debug disabled");
  } else {
    print("debug enabled");
  }
}

/* open enter + exit */
tracepoint:syscalls:sys_enter_open
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  if (@debug == 1) {
    printf("[%s] %d %s ENTER open fname=%s flags=0x%x mode=0%o\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, str(args->filename), args->flags, args->mode, cgroup);
  }

  @open_fname[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_open
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = str(@open_fname[tid]);

  if (@debug == 1) {
    printf("[%s] %d %s EXIT open fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fname, args->ret, cgroup);
  }

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
    // counting the overall bytes used in this file
    @read_bytes[$fname] = sum(0);
    @write_bytes[$fname] = sum(0);

    // counting the overall time this file was open
    @read_time[$fname] = sum(0);
    @write_time[$fname] = sum(0);

    // counting the number of times a file is accesses
    @read_count[$fname] = count();
    @write_count[$fname] = count();
  }

  delete(@open_fname[tid]);
}

/* openat enter + exit */
tracepoint:syscalls:sys_enter_openat
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  if (@debug == 1) {
    printf("[%s] %d %s ENTER openat dfd=%d fname=%s flags=0x%x mode=0%o\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->dfd, str(args->filename), args->flags, args->mode, cgroup);
  }

  @open_fname[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_openat
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = str(@open_fname[tid]);

  if (@debug == 1) {
    printf("[%s] %d %s EXIT openat fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fname, args->ret, cgroup);
  }

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
    // counting the overall bytes used in this file
    @read_bytes[$fname] = sum(0);
    @write_bytes[$fname] = sum(0);

    // counting the overall time this file was open
    @read_time[$fname] = sum(0);
    @write_time[$fname] = sum(0);

    // counting the number of times a file is accesses
    @read_count[$fname] = count();
    @write_count[$fname] = count();
  }

  delete(@open_fname[tid]);
}

/* dup enter + exit */
tracepoint:syscalls:sys_enter_dup
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fildes];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER dup fildes=%d fname=%s\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm, args->fildes, $fname);
  }
  
  @dup_fd[tid] = args->fildes;
}

tracepoint:syscalls:sys_exit_dup
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT dup fd=%d fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm, $fd, $fname, args->ret);
  }

  if (args->ret != -1) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@fname[pid, $fd]);
  delete(@dup_fd[tid]);
}

/* dup2 enter + exit */
tracepoint:syscalls:sys_enter_dup2
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->oldfd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER dup2 oldfd=%d newfd=%d fname=%s\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm,
      args->oldfd, args->newfd, $fname);
  }
  
  @dup_fd[tid] = args->oldfd;
}

tracepoint:syscalls:sys_exit_dup2
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT dup2 fd=%d fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm, $fd, $fname, args->ret);
  }
  
  if (args->ret != -1) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@fname[pid, $fd]);
  delete(@dup_fd[tid]);
}

/* read enter + exit */
tracepoint:syscalls:sys_enter_read
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER read fd=%d fname=%s count=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->fd, $fname, args->count, cgroup);
  }

  @read_fd[tid] = args->fd;
  @read_req_count[tid] = args->count;
  @read_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_read
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT read fd=%d fname=%s ret=%d req=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fd, $fname, args->ret, @read_req_count[tid], cgroup);
  }

  if (args->ret > 0) {
    if ($fname == "") {
      @un_read_bytes[pid, $fd] = sum(args->ret);
      @un_read_time[pid, $fd] = sum(elapsed - @read_start_time[tid]);
      @un_read_count[pid, $fd] = count();
    } else {
      @read_bytes[$fname] = sum(args->ret);
      @read_time[$fname] = sum(elapsed - @read_start_time[tid]);
      @read_count[$fname] = count();
    }
  }

  delete(@read_fd[tid]);
  delete(@read_req_count[tid]);
  delete(@read_start_time[tid]);
}

/* write enter + exit */
tracepoint:syscalls:sys_enter_write
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER write fd=%d fname=%s count=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->fd, $fname, args->count, cgroup);
  }

  @write_fd[tid] = args->fd;
  @write_req_count[tid] = args->count;
  @write_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_write
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT write fd=%d fname=%s ret=%d req=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fd, $fname, args->ret, @write_req_count[tid], cgroup);
  }

  if (args->ret > 0) {
    if ($fname == "") {
      @un_write_bytes[pid, $fd] = sum(args->ret);
      @un_write_time[pid, $fd] = sum(elapsed - @write_start_time[tid]);
      @un_write_count[pid, $fd] = count();
    } else {
      @write_bytes[$fname] = sum(args->ret);
      @write_time[$fname] = sum(elapsed - @write_start_time[tid]);
      @write_count[$fname] = count();
    }
  }

  delete(@write_fd[tid]);
  delete(@write_req_count[tid]);
  delete(@write_start_time[tid]);
}

/* close enter + exit */
tracepoint:syscalls:sys_enter_close
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER close fd=%d fname=%s\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->fd, $fname, cgroup);
  }

  @close_fd[tid] = args->fd;
}

tracepoint:syscalls:sys_exit_close
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @close_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT close fd=%d fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fd, $fname, args->ret, cgroup);
  }

  delete(@close_fd[tid]);
  delete(@fname[pid, $fd]);
}

/* every 60 secunds print the maps */
interval:s:60
{
  // only when not in debugging mode
  if (@debug == 0) {
    printf("--- roatating ---\n");

    print(@read_bytes);
    print(@write_bytes);

    print(@read_time);
    print(@write_time);

    print(@read_count);
    print(@write_count);

    printf("--- roatated ---\n");
  }
}
