#!/usr/bin/env bpftrace
// file: src/bpftrace/cgroups/cgroup_trace_refcount.bt
// usage: sudo bpftrace -o /tmp/tracing.out cgroup_trace_refcount.bt <CGROUP_ID>

BEGIN
{
  @tracked_cgid = (uint64)$1;

  printf("[%s] START tracing file-access events for CGROUP ID %llu\n",
    strftime("%Y-%m-%d %H:%M:%S", nsecs), $1);
}

/* open enter + exit */
tracepoint:syscalls:sys_enter_open
/ cgroup == @tracked_cgid /
{
  @open_fname[tid] = str(args->filename);
}

tracepoint:syscalls:sys_exit_open
/ cgroup == @tracked_cgid /
{
  $fname = @open_fname[tid];

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
    @ref_count[pid, args->ret] = 1;  // initialize ref count

    // initialize counting maps
    @read_bytes[$fname] = sum(0);
    @write_bytes[$fname] = sum(0);
    @read_time[$fname] = sum(0);
    @write_time[$fname] = sum(0);
    @read_count[$fname] = count();
    @write_count[$fname] = count();
  }

  delete(@open_fname[tid]);
}

/* openat enter + exit */
tracepoint:syscalls:sys_enter_openat
/ cgroup == @tracked_cgid /
{
  @open_fname[tid] = str(args->filename);
}

tracepoint:syscalls:sys_exit_openat
/ cgroup == @tracked_cgid /
{
  $fname = @open_fname[tid];

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
    @ref_count[pid, args->ret] = 1;  // initialize ref count

    // initialize counting maps
    @read_bytes[$fname] = sum(0);
    @write_bytes[$fname] = sum(0);
    @read_time[$fname] = sum(0);
    @write_time[$fname] = sum(0);
    @read_count[$fname] = count();
    @write_count[$fname] = count();
  }

  delete(@open_fname[tid]);
}

/* dup enter + exit */
tracepoint:syscalls:sys_enter_dup
/ cgroup == @tracked_cgid /
{
  @dup_fd[tid] = args->fildes;
}

tracepoint:syscalls:sys_exit_dup
/ cgroup == @tracked_cgid /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];

  if (args->ret != -1) {
    @fname[pid, args->ret] = $fname;

    // increment ref count on original fd if exists
    $old_ref = @ref_count[pid, $fd];
    if ($old_ref == 0) {
      $old_ref = 1;
    }
    @ref_count[pid, $fd] = $old_ref + 1;
    @ref_count[pid, args->ret] = $old_ref + 1;
  }

  delete(@dup_fd[tid]);
}

/* dup2 enter + exit */
tracepoint:syscalls:sys_enter_dup2
/ cgroup == @tracked_cgid /
{
  @dup_fd[tid] = args->oldfd;
}

tracepoint:syscalls:sys_exit_dup2
/ cgroup == @tracked_cgid /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];

  if (args->ret != -1) {
    @fname[pid, args->ret] = $fname;

    // increment ref count on original fd if exists
    $old_ref = @ref_count[pid, $fd];
    if ($old_ref == 0) {
      $old_ref = 1;
    }
    @ref_count[pid, $fd] = $old_ref + 1;
    @ref_count[pid, args->ret] = $old_ref + 1;
  }

  delete(@dup_fd[tid]);
}

/* read enter + exit */
tracepoint:syscalls:sys_enter_read
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];

  @read_fd[tid] = args->fd;
  @read_req_count[tid] = args->count;
  @read_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_read
/ cgroup == @tracked_cgid /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];

  if (args->ret > 0) {
    if ($fname == "") {
      @un_read_bytes[pid, $fd] = sum(args->ret);
      @un_read_time[pid, $fd] = sum(elapsed - @read_start_time[tid]);
      @un_read_count[pid, $fd] = count();
    } else {
      @read_bytes[$fname] = sum(args->ret);
      @read_time[$fname] = sum(elapsed - @read_start_time[tid]);
      @read_count[$fname] = count();
    }
  }

  delete(@read_fd[tid]);
  delete(@read_req_count[tid]);
  delete(@read_start_time[tid]);
}

/* write enter + exit */
tracepoint:syscalls:sys_enter_write
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];

  @write_fd[tid] = args->fd;
  @write_req_count[tid] = args->count;
  @write_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_write
/ cgroup == @tracked_cgid /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];

  if (args->ret > 0) {
    if ($fname == "") {
      @un_write_bytes[pid, $fd] = sum(args->ret);
      @un_write_time[pid, $fd] = sum(elapsed - @write_start_time[tid]);
      @un_write_count[pid, $fd] = count();
    } else {
      @write_bytes[$fname] = sum(args->ret);
      @write_time[$fname] = sum(elapsed - @write_start_time[tid]);
      @write_count[$fname] = count();
    }
  }

  delete(@write_fd[tid]);
  delete(@write_req_count[tid]);
  delete(@write_start_time[tid]);
}

/* close enter + exit */
tracepoint:syscalls:sys_enter_close
/ cgroup == @tracked_cgid /
{
  @close_fd[tid] = args->fd;
}

tracepoint:syscalls:sys_exit_close
/ cgroup == @tracked_cgid /
{
  $fd = @close_fd[tid];
  $ref = @ref_count[pid, $fd];

  if ($ref > 1) {
    @ref_count[pid, $fd] = $ref - 1;  // decrement ref count
  } else {
    delete(@ref_count[pid, $fd]);    // no more references, delete
    delete(@fname[pid, $fd]);        // delete filename mapping
  }

  delete(@close_fd[tid]);
}
