#!/usr/bin/env bpftrace
// file: src/bpftrace/cgroups/cgroup_trace.bt
// usage: sudo bpftrace -o /tmp/tracing.out cgroup_trace.bt <CGROUP_ID>
// log format: [timestamp] { pid=[pid] tid=[tid] proc=[command] } { [ENTER|EXIT] [operand] } { [key=value] }

BEGIN
{
  @tracked_cgid = (uint64)$1;
  printf("%s START tracing file-access events for CGROUP ID %llu\n", strftime("%Y-%m-%d %H:%M:%S", nsecs), $1);
}

// ----- Metadata Extraction Syscalls -----
/* creat enter + exit */
tracepoint:syscalls:sys_enter_creat
/ cgroup == @tracked_cgid /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER creat } { fname=%s mode=0%o }\n", nsecs, pid, tid, comm, str(args->pathname), args->mode);
  @open_fname[tid] = args->pathname;
}

tracepoint:syscalls:sys_exit_creat
/ cgroup == @tracked_cgid /
{
  $fname = str(@open_fname[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT creat } { fname=%s ret=0x%lx }\n", nsecs, pid, tid, comm, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@open_fname[tid]);
}

/* open enter + exit */
tracepoint:syscalls:sys_enter_open
/ cgroup == @tracked_cgid /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER open } { fname=%s flags=0x%x mode=0%o }\n", nsecs, pid, tid, comm, str(args->filename), args->flags, args->mode);
  @open_fname[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_open
/ cgroup == @tracked_cgid /
{
  $fname = str(@open_fname[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT open } { fname=%s ret=0x%lx }\n", nsecs, pid, tid, comm, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@open_fname[tid]);
}

/* openat enter + exit */
tracepoint:syscalls:sys_enter_openat
/ cgroup == @tracked_cgid /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER openat } { dfd=%d fname=%s flags=0x%x mode=0%o }\n", nsecs, pid, tid, comm, args->dfd, str(args->filename), args->flags, args->mode);
  @open_fname[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_openat
/ cgroup == @tracked_cgid /
{
  $fname = str(@open_fname[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT openat } { fname=%s ret=0x%lx }\n", nsecs, pid, tid, comm, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@open_fname[tid]);
}

/* dup enter + exit */
tracepoint:syscalls:sys_enter_dup
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fildes];
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER dup } { fildes=%d fname=%s }\n", nsecs, pid, tid, comm, args->fildes, $fname);
  @dup_fd[tid] = args->fildes;
}

tracepoint:syscalls:sys_exit_dup
/ cgroup == @tracked_cgid /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT dup } { fd=%d fname=%s ret=0x%lx }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@dup_fd[tid]);
}

/* dup2 enter + exit */
tracepoint:syscalls:sys_enter_dup2
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->oldfd];
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER dup2 } { oldfd=%d newfd=%d fname=%s }\n", nsecs, pid, tid, comm, args->oldfd, args->newfd, $fname);
  @dup_fd[tid] = args->oldfd;
}

tracepoint:syscalls:sys_exit_dup2
/ cgroup == @tracked_cgid /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT dup2 } { fd=%d fname=%s ret=0x%lx }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret);
  
  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@dup_fd[tid]);
}

/* dup3 enter + exit */
tracepoint:syscalls:sys_enter_dup3
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->oldfd];
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER dup3 } { oldfd=%d newfd=%d fname=%s }\n", nsecs, pid, tid, comm, args->oldfd, args->newfd, $fname);
  @dup_fd[tid] = args->oldfd;
}

tracepoint:syscalls:sys_exit_dup3
/ cgroup == @tracked_cgid /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT dup3 } { fd=%d fname=%s ret=0x%lx }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret);
  
  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@dup_fd[tid]);
}

/* statfs enter + exit */
tracepoint:syscalls:sys_enter_statfs
/ cgroup == @tracked_cgid /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER statfs } { fname=%s }\n", nsecs, pid, tid, comm, str(args->pathname));
  @stats_path[tid] = args->pathname;
}

tracepoint:syscalls:sys_exit_statfs
/ cgroup == @tracked_cgid /
{
  $fname = str(@stats_path[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT statfs } { fname=%s }\n", nsecs, pid, tid, comm, $fname);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

/* statx enter + exit */
tracepoint:syscalls:sys_enter_statx
/ cgroup == @tracked_cgid /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER statx } { dfd=%d fname=%s flags=0x%08lx }\n", nsecs, pid, tid, comm, args->dfd, str(args->filename), args->flags);
  @stats_path[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_statx
/ cgroup == @tracked_cgid /
{
  $fname = str(@stats_path[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT statx } { fname=%s }\n", nsecs, pid, tid, comm, $fname);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

/* newstat enter + exit */
tracepoint:syscalls:sys_enter_newstat
/ cgroup == @tracked_cgid /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER newstat } { fname=%s }\n", nsecs, pid, tid, comm, str(args->filename));
  @stats_path[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_newstat
/ cgroup == @tracked_cgid /
{
  $fname = str(@stats_path[tid]);

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT newstat } { fname=%s }\n", nsecs, pid, tid, comm, $fname);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

/* newlstat enter + exit */
tracepoint:syscalls:sys_enter_newlstat
/ cgroup == @tracked_cgid /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER newlstat } { fname=%s }\n", nsecs, pid, tid, comm, str(args->filename));
  @stats_path[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_newlstat
/ cgroup == @tracked_cgid /
{
  $fname = str(@stats_path[tid]);
  
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT newlstat } { fname=%s }\n", nsecs, pid, tid, comm, $fname);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

// ----- I/O Operation Syscalls -----
/* read enter + exit */
tracepoint:syscalls:sys_enter_read
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];

  @read_fd[tid] = args->fd;
  @read_req_count[tid] = args->count;
  @read_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER read } { fd=%d fname=%s count=%d }\n", nsecs, pid, tid, comm, args->fd, $fname, args->count);
}

tracepoint:syscalls:sys_exit_read
/ cgroup == @tracked_cgid /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @read_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT read } { fd=%d fname=%s ret=0x%lx req=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, @read_req_count[tid], $latency);

  delete(@read_fd[tid]);
  delete(@read_req_count[tid]);
  delete(@read_start_time[tid]);
}

/* write enter + exit */
tracepoint:syscalls:sys_enter_write
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];

  @write_fd[tid] = args->fd;
  @write_req_count[tid] = args->count;
  @write_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER write } { fd=%d fname=%s count=%d }\n", nsecs, pid, tid, comm, args->fd, $fname, args->count);
}

tracepoint:syscalls:sys_exit_write
/ cgroup == @tracked_cgid /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @write_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT write } { fd=%d fname=%s ret=0x%lx req=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, @write_req_count[tid], $latency);

  delete(@write_fd[tid]);
  delete(@write_req_count[tid]);
  delete(@write_start_time[tid]);
}

/* pread64 enter + exit */
tracepoint:syscalls:sys_enter_pread64
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];

  @read_fd[tid] = args->fd;
  @read_req_count[tid] = args->count;
  @read_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER pread64 } { fd=%d fname=%s count=%d pos=0x%08lx }\n", nsecs, pid, tid, comm, args->fd, $fname, args->count, args->pos);
}

tracepoint:syscalls:sys_exit_pread64
/ cgroup == @tracked_cgid /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @read_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT pread64 } { fd=%d fname=%s ret=0x%lx req=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, @read_req_count[tid], $latency);

  delete(@read_fd[tid]);
  delete(@read_req_count[tid]);
  delete(@read_start_time[tid]);
}

/* pwrite64 enter + exit */
tracepoint:syscalls:sys_enter_pwrite64
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];

  @write_fd[tid] = args->fd;
  @write_req_count[tid] = args->count;
  @write_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER pwrite64 } { fd=%d fname=%s count=%d pos=0x%08lx }\n", nsecs, pid, tid, comm, args->fd, $fname, args->count, args->pos);
}

tracepoint:syscalls:sys_exit_pwrite64
/ cgroup == @tracked_cgid /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @write_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT pwrite64 } { fd=%d fname=%s ret=0x%lx req=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, @write_req_count[tid], $latency);

  delete(@write_fd[tid]);
  delete(@write_req_count[tid]);
  delete(@write_start_time[tid]);
}

/* readv enter + exit */
tracepoint:syscalls:sys_enter_readv
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];

  @read_fd[tid] = args->fd;
  @read_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER readv } { fd=%d fname=%s vlen=0x%08lx }\n", nsecs, pid, tid, comm, args->fd, $fname, args->vlen);
}

tracepoint:syscalls:sys_exit_readv
/ cgroup == @tracked_cgid /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @read_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT readv } { fd=%d fname=%s ret=0x%lx latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);

  delete(@read_fd[tid]);
  delete(@read_start_time[tid]);
}

/* writev enter + exit */
tracepoint:syscalls:sys_enter_writev
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];

  @write_fd[tid] = args->fd;
  @write_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER writev } { fd=%d fname=%s vlen=0x%08lx }\n", nsecs, pid, tid, comm, args->fd, $fname, args->vlen);
}

tracepoint:syscalls:sys_exit_writev
/ cgroup == @tracked_cgid /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @write_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT writev } { fd=%d fname=%s ret=0x%lx latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);

  delete(@write_fd[tid]);
  delete(@write_start_time[tid]);
}

/* preadv enter + exit */
tracepoint:syscalls:sys_enter_preadv
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];

  @read_fd[tid] = args->fd;
  @read_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER preadv } { fd=%d fname=%s vlen=0x%08lx pos_l=0x%08lx pos_h=0x%08lx }\n", nsecs, pid, tid, comm, args->fd, $fname, args->vlen, args->pos_l, args->pos_h);
}

tracepoint:syscalls:sys_exit_preadv
/ cgroup == @tracked_cgid /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @read_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT preadv } { fd=%d fname=%s ret=0x%lx latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);

  delete(@read_fd[tid]);
  delete(@read_start_time[tid]);
}

/* pwritev enter + exit */
tracepoint:syscalls:sys_enter_pwritev
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];

  @write_fd[tid] = args->fd;
  @write_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER pwritev } { fd=%d fname=%s vlen=0x%08lx pos_l=0x%08lx pos_h=0x%08lx }\n", nsecs, pid, tid, comm, args->fd, $fname, args->vlen, args->pos_l, args->pos_h);
}

tracepoint:syscalls:sys_exit_pwritev
/ cgroup == @tracked_cgid /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @write_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT pwritev } { fd=%d fname=%s ret=0x%lx latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);

  delete(@write_fd[tid]);
  delete(@write_start_time[tid]);
}

/* mmap enter + exit */
tracepoint:syscalls:sys_enter_mmap
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];

  // store mmap inputs for exit
  @mmap_fd[tid] = args->fd;
  @mmap_len[tid] = args->len;
  @mmap_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER mmap } { fd=%d fname=%s addr=0x%lx len=%lu prot=0x%x flags=0x%x off: 0x%081x }\n", nsecs, pid, tid, comm, args->fd, $fname, args->addr, args->len, args->prot, args->flags, args->off);
}

tracepoint:syscalls:sys_exit_mmap
/ cgroup == @tracked_cgid /
{
  $fd   = @mmap_fd[tid]; 
  $fname = @fname[pid, $fd];
  $len  = @mmap_len[tid];
  $latency = elapsed - @mmap_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT mmap } { fd=%d fname=%s ret=0x%lx len=%llu latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $len, $latency);

  delete(@mmap_fd[tid]);
  delete(@mmap_len[tid]);
  delete(@mmap_start_time[tid]);
}

/* close enter + exit */
tracepoint:syscalls:sys_enter_close
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER close } { fd=%d fname=%s }\n", nsecs, pid, tid, comm, args->fd, $fname);
  @close_fd[tid] = args->fd;
}

tracepoint:syscalls:sys_exit_close
/ cgroup == @tracked_cgid /
{
  $fd = @close_fd[tid];
  $fname = @fname[pid, $fd];
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT close } { fd=%d fname=%s ret=0x%lx }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret);

  delete(@close_fd[tid]);
}
