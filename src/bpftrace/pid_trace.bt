#!/usr/bin/env bpftrace
// file: src/bpftrace/pid_trace.bt
// sudo bpftrace -o /tmp/tracing.out pid_trace.bt <PID>
// log format: [timestamp] { pid=[pid] tid=[tid] proc=[command] } { [ENTER|EXIT] [operand] } { [key=value] }

BEGIN
{
  @fname[$1, 0] = "STDIN";
  @fname[$1, 1] = "STDOUT";
  @fname[$1, 2] = "STDERR";
  @tracked[$1] = 1;
  printf("%s START tracing file-access events for PID %llu\n", strftime("%Y-%m-%d %H:%M:%S", nsecs), $1);
}

/* ----- Child Process Tracing ----- */
/* when we see a fork, if parent is tracked then also track child */
tracepoint:sched:sched_process_fork
/ @tracked[args->parent_pid] /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER fork } { pid=%d comm=%s }\n", nsecs, args->parent_pid, args->parent_comm, args->child_pid, args->child_comm);

  @fname[pid, 0] = "STDIN";
  @fname[pid, 1] = "STDOUT";
  @fname[pid, 2] = "STDERR";
  @tracked[args->child_pid] = 1;
}

/* when exec happens, if old_pid tracked ensure that the new pid is also tracked */
tracepoint:sched:sched_process_exec
/ @tracked[args->old_pid] /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER exec } { pid=%d fname=%s }\n", nsecs, args->old_pid, tid, comm, args->pid, str(args->filename));

  @fname[pid, 0] = "STDIN";
  @fname[pid, 1] = "STDOUT";
  @fname[pid, 2] = "STDERR";
  @tracked[args->pid] = 1;
}

/* cleanup process fname table and untrack process */
tracepoint:sched:sched_process_exit
/ @tracked[pid] /
{
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT process }\n", nsecs, pid, tid, comm);

  delete(@fname[pid, 0]);
  delete(@fname[pid, 1]);
  delete(@fname[pid, 2]);
  delete(@tracked[pid]);
}

// ----- Metadata Extraction Syscalls -----
/* creat enter + exit */
tracepoint:syscalls:sys_enter_creat
/ @tracked[pid] /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER creat } { fname=%s mode=0%o }\n", nsecs, pid, tid, comm, str(args->pathname), args->mode);
  @open_fname[tid] = args->pathname;
}

tracepoint:syscalls:sys_exit_creat
/ @tracked[pid] /
{
  $fname = str(@open_fname[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT creat } { fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@open_fname[tid]);
}

/* open enter + exit */
tracepoint:syscalls:sys_enter_open
/ @tracked[pid] /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER open } { fname=%s flags=0x%x mode=0%o }\n", nsecs, pid, tid, comm, str(args->filename), args->flags, args->mode);
  @open_fname[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_open
/ @tracked[pid] /
{
  $fname = str(@open_fname[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT open } { fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@open_fname[tid]);
}

/* openat enter + exit */
tracepoint:syscalls:sys_enter_openat
/ @tracked[pid] /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER openat } { dfd=%d fname=%s flags=0x%x mode=0%o }\n", nsecs, pid, tid, comm, args->dfd, str(args->filename), args->flags, args->mode);
  @open_fname[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_openat
/ @tracked[pid] /
{
  $fname = str(@open_fname[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT openat } { fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@open_fname[tid]);
}

/* dup enter + exit */
tracepoint:syscalls:sys_enter_dup
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fildes];
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER dup } { fildes=%d fname=%s }\n", nsecs, pid, tid, comm, args->fildes, $fname);
  @dup_fd[tid] = args->fildes;
}

tracepoint:syscalls:sys_exit_dup
/ @tracked[pid] /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT dup } { fd=%d fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@dup_fd[tid]);
}

/* dup2 enter + exit */
tracepoint:syscalls:sys_enter_dup2
/ @tracked[pid] /
{
  $fname = @fname[pid, args->oldfd];
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER dup2 } { oldfd=%d newfd=%d fname=%s }\n", nsecs, pid, tid, comm, args->oldfd, args->newfd, $fname);
  @dup_fd[tid] = args->oldfd;
}

tracepoint:syscalls:sys_exit_dup2
/ @tracked[pid] /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT dup2 } { fd=%d fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret);
  
  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@dup_fd[tid]);
}

/* dup3 enter + exit */
tracepoint:syscalls:sys_enter_dup3
/ @tracked[pid] /
{
  $fname = @fname[pid, args->oldfd];
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER dup3 } { oldfd=%d newfd=%d fname=%s }\n", nsecs, pid, tid, comm, args->oldfd, args->newfd, $fname);
  @dup_fd[tid] = args->oldfd;
}

tracepoint:syscalls:sys_exit_dup3
/ @tracked[pid] /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT dup3 } { fd=%d fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret);
  
  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@dup_fd[tid]);
}

/* statfs enter + exit */
tracepoint:syscalls:sys_enter_statfs
/ @tracked[pid] /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER statfs } { fname=%s }\n", nsecs, pid, tid, comm, str(args->pathname));
  @stats_path[tid] = args->pathname;
}

tracepoint:syscalls:sys_exit_statfs
/ @tracked[pid] /
{
  $fname = str(@stats_path[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT statfs } { fname=%s }\n", nsecs, pid, tid, comm, $fname);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

/* statx enter + exit */
tracepoint:syscalls:sys_enter_statx
/ @tracked[pid] /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER statx } { dfd=%d fname=%s flags=0x%08lx }\n", nsecs, pid, tid, comm, args->dfd, str(args->filename), args->flags);
  @stats_path[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_statx
/ @tracked[pid] /
{
  $fname = str(@stats_path[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT statx } { fname=%s }\n", nsecs, pid, tid, comm, $fname);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

/* newstat enter + exit */
tracepoint:syscalls:sys_enter_newstat
/ @tracked[pid] /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER newstat } { fname=%s }\n", nsecs, pid, tid, comm, str(args->filename));
  @stats_path[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_newstat
/ @tracked[pid] /
{
  $fname = str(@stats_path[tid]);

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT newstat } { fname=%s }\n", nsecs, pid, tid, comm, $fname);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

/* newlstat enter + exit */
tracepoint:syscalls:sys_enter_newlstat
/ @tracked[pid] /
{
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER newlstat } { fname=%s }\n", nsecs, pid, tid, comm, str(args->filename));
  @stats_path[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_newlstat
/ @tracked[pid] /
{
  $fname = str(@stats_path[tid]);
  
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT newlstat } { fname=%s }\n", nsecs, pid, tid, comm, $fname);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

// ----- I/O Operation Syscalls -----
/* read enter + exit */
tracepoint:syscalls:sys_enter_read
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  @read_fd[tid] = args->fd;
  @read_req_count[tid] = args->count;
  @read_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER read } { fd=%d fname=%s count=%d }\n", nsecs, pid, tid, comm, args->fd, $fname, args->count);
}

tracepoint:syscalls:sys_exit_read
/ @tracked[pid] /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @read_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT read } { fd=%d fname=%s ret=%d req=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, @read_req_count[tid], $latency);

  delete(@read_fd[tid]);
  delete(@read_req_count[tid]);
  delete(@read_start_time[tid]);
}

/* write enter + exit */
tracepoint:syscalls:sys_enter_write
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  @write_fd[tid] = args->fd;
  @write_req_count[tid] = args->count;
  @write_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER write } { fd=%d fname=%s count=%d }\n", nsecs, pid, tid, comm, args->fd, $fname, args->count);
}

tracepoint:syscalls:sys_exit_write
/ @tracked[pid] /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @write_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT write } { fd=%d fname=%s ret=%d req=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, @write_req_count[tid], $latency);

  delete(@write_fd[tid]);
  delete(@write_req_count[tid]);
  delete(@write_start_time[tid]);
}

/* pread64 enter + exit */
tracepoint:syscalls:sys_enter_pread64
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  @read_fd[tid] = args->fd;
  @read_req_count[tid] = args->count;
  @read_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER pread64 } { fd=%d fname=%s count=%d pos=0x%08lx }\n", nsecs, pid, tid, comm, args->fd, $fname, args->count, args->pos);
}

tracepoint:syscalls:sys_exit_pread64
/ @tracked[pid] /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @read_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT pread64 } { fd=%d fname=%s ret=%d req=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, @read_req_count[tid], $latency);

  delete(@read_fd[tid]);
  delete(@read_req_count[tid]);
  delete(@read_start_time[tid]);
}

/* pwrite64 enter + exit */
tracepoint:syscalls:sys_enter_pwrite64
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  @write_fd[tid] = args->fd;
  @write_req_count[tid] = args->count;
  @write_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER pwrite64 } { fd=%d fname=%s count=%d pos=0x%08lx }\n", nsecs, pid, tid, comm, args->fd, $fname, args->count, args->pos);
}

tracepoint:syscalls:sys_exit_pwrite64
/ @tracked[pid] /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @write_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT pwrite64 } { fd=%d fname=%s ret=%d req=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, @write_req_count[tid], $latency);

  delete(@write_fd[tid]);
  delete(@write_req_count[tid]);
  delete(@write_start_time[tid]);
}

/* readv enter + exit */
tracepoint:syscalls:sys_enter_readv
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  @read_fd[tid] = args->fd;
  @read_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER readv } { fd=%d fname=%s vlen=0x%08lx }\n", nsecs, pid, tid, comm, args->fd, $fname, args->vlen);
}

tracepoint:syscalls:sys_exit_readv
/ @tracked[pid] /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @read_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT readv } { fd=%d fname=%s ret=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);

  delete(@read_fd[tid]);
  delete(@read_start_time[tid]);
}

/* writev enter + exit */
tracepoint:syscalls:sys_enter_writev
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  @write_fd[tid] = args->fd;
  @write_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER writev } { fd=%d fname=%s vlen=0x%08lx }\n", nsecs, pid, tid, comm, args->fd, $fname, args->vlen);
}

tracepoint:syscalls:sys_exit_writev
/ @tracked[pid] /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @write_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT writev } { fd=%d fname=%s ret=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);

  delete(@write_fd[tid]);
  delete(@write_start_time[tid]);
}

/* preadv enter + exit */
tracepoint:syscalls:sys_enter_preadv
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  @read_fd[tid] = args->fd;
  @read_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER preadv } { fd=%d fname=%s vlen=0x%08lx pos_l=0x%08lx pos_h=0x%08lx }\n", nsecs, pid, tid, comm, args->fd, $fname, args->vlen, args->pos_l, args->pos_h);
}

tracepoint:syscalls:sys_exit_preadv
/ @tracked[pid] /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @read_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT preadv } { fd=%d fname=%s ret=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);

  delete(@read_fd[tid]);
  delete(@read_start_time[tid]);
}

/* pwritev enter + exit */
tracepoint:syscalls:sys_enter_pwritev
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  @write_fd[tid] = args->fd;
  @write_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER pwritev } { fd=%d fname=%s vlen=0x%08lx pos_l=0x%08lx pos_h=0x%08lx }\n", nsecs, pid, tid, comm, args->fd, $fname, args->vlen, args->pos_l, args->pos_h);
}

tracepoint:syscalls:sys_exit_pwritev
/ @tracked[pid] /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @write_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT pwritev } { fd=%d fname=%s ret=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);

  delete(@write_fd[tid]);
  delete(@write_start_time[tid]);
}

/* mmap enter + exit */
tracepoint:syscalls:sys_enter_mmap
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  // store mmap inputs for exit
  @mmap_fd[tid] = args->fd;
  @mmap_len[tid] = args->len;
  @mmap_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { ENTER mmap } { fd=%d fname=%s addr=0x%lx len=%lu prot=0x%x flags=0x%x off: 0x%081x }\n", nsecs, pid, tid, comm, args->fd, $fname, args->addr, args->len, args->prot, args->flags, args->off);
}

tracepoint:syscalls:sys_exit_mmap
/ @tracked[pid] /
{
  $fd   = @mmap_fd[tid]; 
  $fname = @fname[pid, $fd];
  $len  = @mmap_len[tid];
  $latency = elapsed - @mmap_start_time[tid];

  printf("%llu { pid=%d tid=%d proc=%s } { EXIT mmap } { fd=%d fname=%s ret=%d len=%llu latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $len, $latency);

  delete(@mmap_fd[tid]);
  delete(@mmap_len[tid]);
  delete(@mmap_start_time[tid]);
}

/* close enter + exit */
tracepoint:syscalls:sys_enter_close
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];
  printf("%llu { pid=%d tid=%d proc=%s } { ENTER close } { fd=%d fname=%s }\n", nsecs, pid, tid, comm, args->fd, $fname);
  @close_fd[tid] = args->fd;
}

tracepoint:syscalls:sys_exit_close
/ @tracked[pid] /
{
  $fd = @close_fd[tid];
  $fname = @fname[pid, $fd];
  printf("%llu { pid=%d tid=%d proc=%s } { EXIT close } { fd=%d fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret);

  delete(@close_fd[tid]);
}
