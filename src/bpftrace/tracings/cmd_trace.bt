#!/usr/bin/env bpftrace
// file: src/bpftrace/tracings/cmd_trace.bt
// sudo bpftrace -c "./myapp arg1 arg2" -o /tmp/tracing.out cmd_trace.bt <DEBUG>

BEGIN
{
  @fname[cpid, 0] = "STDIN";
  @fname[cpid, 1] = "STDOUT";
  @fname[cpid, 2] = "STDERR";
  @tracked[cpid] = 1;
  @debug = (int8)$1;

  printf("[%s] START tracing file-access events for CPID %llu\n", 
    strftime("%Y-%m-%d %H:%M:%S", nsecs), cpid);
  if (@debug == 0) {
    print("debug disabled");
  } else {
    print("debug enabled");
  }
}

/* ----- Child Process Tracing ----- */
/* when we see a fork, if parent is tracked then also track child */
tracepoint:sched:sched_process_fork
/ @tracked[args->parent_pid] /
{
  printf("[%s] %d %s FORK -> %d %s\n",
    strftime("%Y-%m-%d %H:%M:%S", nsecs),
    args->parent_pid, args->parent_comm, args->child_pid, args->child_comm);

  @fname[pid, 0] = "STDIN";
  @fname[pid, 1] = "STDOUT";
  @fname[pid, 2] = "STDERR";
  @tracked[args->child_pid] = 1;
}

/* when exec happens, if old_pid tracked ensure that the new pid is also tracked */
tracepoint:sched:sched_process_exec
/ @tracked[args->old_pid] /
{
  printf("[%s] %d %s EXEC %d %s\n",
    strftime("%Y-%m-%d %H:%M:%S", nsecs),
    args->old_pid, comm, args->pid, str(args->filename));

  @fname[pid, 0] = "STDIN";
  @fname[pid, 1] = "STDOUT";
  @fname[pid, 2] = "STDERR";
  @tracked[args->pid] = 1;
}

/* cleanup process fname table and untrack process */
tracepoint:sched:sched_process_exit
/ @tracked[pid] /
{
  printf("[%s] %d %s EXIT_PROCESS\n", strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm);

  delete(@fname[pid, 0]);
  delete(@fname[pid, 1]);
  delete(@fname[pid, 2]);
  delete(@tracked[pid]);
}

// ----- Metadata Extraction Syscalls -----
/* creat enter + exit */
tracepoint:syscalls:sys_enter_creat
/ @tracked[pid] /
{
  if (@debug == 1) {
    printf("[%s] %d %s ENTER creat fname=%s mode=0%o\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, str(args->pathname), args->mode);
  }

  @open_fname[tid] = args->pathname;
}

tracepoint:syscalls:sys_exit_creat
/ @tracked[pid] /
{
  $fname = str(@open_fname[tid]);

  if (@debug == 1) {
    printf("[%s] %d %s EXIT creat fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fname, args->ret);
  }

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
    // counting the overall bytes used in this file
    @read_bytes[$fname] = sum(0);
    @write_bytes[$fname] = sum(0);

    // counting the overall time this file was open
    @read_time[$fname] = sum(0);
    @write_time[$fname] = sum(0);

    // counting the number of times a file is accesses
    @read_count[$fname] = count();
    @write_count[$fname] = count();
  }

  delete(@open_fname[tid]);
}

/* open enter + exit */
tracepoint:syscalls:sys_enter_open
/ @tracked[pid] /
{
  if (@debug == 1) {
    printf("[%s] %d %s ENTER open fname=%s flags=0x%x mode=0%o\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, str(args->filename), args->flags, args->mode);
  }

  @open_fname[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_open
/ @tracked[pid] /
{
  $fname = str(@open_fname[tid]);

  if (@debug == 1) {
    printf("[%s] %d %s EXIT open fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fname, args->ret);
  }

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
    // counting the overall bytes used in this file
    @read_bytes[$fname] = sum(0);
    @write_bytes[$fname] = sum(0);

    // counting the overall time this file was open
    @read_time[$fname] = sum(0);
    @write_time[$fname] = sum(0);

    // counting the number of times a file is accesses
    @read_count[$fname] = count();
    @write_count[$fname] = count();
  }

  delete(@open_fname[tid]);
}

/* openat enter + exit */
tracepoint:syscalls:sys_enter_openat
/ @tracked[pid] /
{
  if (@debug == 1) {
    printf("[%s] %d %s ENTER openat dfd=%d fname=%s flags=0x%x mode=0%o\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->dfd, str(args->filename), args->flags, args->mode);
  }

  @open_fname[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_openat
/ @tracked[pid] /
{
  $fname = str(@open_fname[tid]);

  if (@debug == 1) {
    printf("[%s] %d %s EXIT openat fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fname, args->ret);
  }

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
    // counting the overall bytes used in this file
    @read_bytes[$fname] = sum(0);
    @write_bytes[$fname] = sum(0);

    // counting the overall time this file was open
    @read_time[$fname] = sum(0);
    @write_time[$fname] = sum(0);

    // counting the number of times a file is accesses
    @read_count[$fname] = count();
    @write_count[$fname] = count();
  }

  delete(@open_fname[tid]);
}

/* dup enter + exit */
tracepoint:syscalls:sys_enter_dup
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fildes];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER dup fildes=%d fname=%s\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm, args->fildes, $fname);
  }
  
  @dup_fd[tid] = args->fildes;
}

tracepoint:syscalls:sys_exit_dup
/ @tracked[pid] /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT dup fd=%d fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm, $fd, $fname, args->ret);
  }

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@dup_fd[tid]);
}

/* dup2 enter + exit */
tracepoint:syscalls:sys_enter_dup2
/ @tracked[pid] /
{
  $fname = @fname[pid, args->oldfd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER dup2 oldfd=%d newfd=%d fname=%s\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm,
      args->oldfd, args->newfd, $fname);
  }
  
  @dup_fd[tid] = args->oldfd;
}

tracepoint:syscalls:sys_exit_dup2
/ @tracked[pid] /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT dup2 fd=%d fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm, $fd, $fname, args->ret);
  }
  
  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@dup_fd[tid]);
}

/* dup3 enter + exit */
tracepoint:syscalls:sys_enter_dup3
/ @tracked[pid] /
{
  $fname = @fname[pid, args->oldfd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER dup3 oldfd=%d newfd=%d fname=%s\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm,
      args->oldfd, args->newfd, $fname);
  }
  
  @dup_fd[tid] = args->oldfd;
}

tracepoint:syscalls:sys_exit_dup3
/ @tracked[pid] /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT dup3 fd=%d fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm, $fd, $fname, args->ret);
  }
  
  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@dup_fd[tid]);
}

/* statfs enter + exit */
tracepoint:syscalls:sys_enter_statfs
/ @tracked[pid] /
{
  if (@debug == 1) {
    printf("[%s] %d %s ENTER statfs fname=%s\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm,
      str(args->pathname));
  }

  @stats_path[tid] = args->pathname;
}

tracepoint:syscalls:sys_exit_statfs
/ @tracked[pid] /
{
  $fname = str(@stats_path[tid]);

  if (@debug == 1) {
    printf("[%s] %d %s EXIT statfs fname=%s\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm,
      $fname);
  }

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

/* statx enter + exit */
tracepoint:syscalls:sys_enter_statx
/ @tracked[pid] /
{
  if (@debug == 1) {
    printf("[%s] %d %s ENTER statx dfd=%d fname=%s flags=0x%08lx\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm,
      args->dfd, str(args->filename), args->flags);
  }

  @stats_path[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_statx
/ @tracked[pid] /
{
  $fname = str(@stats_path[tid]);

  if (@debug == 1) {
    printf("[%s] %d %s EXIT statx fname=%s\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm,
      $fname);
  }

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

/* newstat enter + exit */
tracepoint:syscalls:sys_enter_newstat
/ @tracked[pid] /
{
  if (@debug == 1) {
    printf("[%s] %d %s ENTER newstat fname=%s\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm,
      str(args->filename));
  }

  @stats_path[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_newstat
/ @tracked[pid] /
{
  $fname = str(@stats_path[tid]);

  if (@debug == 1) {
    printf("[%s] %d %s EXIT newstat fname=%s\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm,
      $fname);
  }

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

/* newlstat enter + exit */
tracepoint:syscalls:sys_enter_newlstat
/ @tracked[pid] /
{
  if (@debug == 1) {
    printf("[%s] %d %s ENTER newlstat fname=%s\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm,
      str(args->filename));
  }

  @stats_path[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_newlstat
/ @tracked[pid] /
{
  $fname = str(@stats_path[tid]);

  if (@debug == 1) {
    printf("[%s] %d %s EXIT newlstat fname=%s\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm,
      $fname);
  }

  if (args->ret != -1) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

// ----- I/O Operation Syscalls -----
/* read enter + exit */
tracepoint:syscalls:sys_enter_read
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER read fd=%d fname=%s count=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->fd, $fname, args->count);
  }

  @read_fd[tid] = args->fd;
  @read_req_count[tid] = args->count;
  @read_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_read
/ @tracked[pid] /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT read fd=%d fname=%s ret=%d req=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fd, $fname, args->ret, @read_req_count[tid]);
  }

  if (args->ret > 0) {
    if ($fname == "") {
      @un_read_bytes[pid, $fd] = sum(args->ret);
      @un_read_time[pid, $fd] = sum(elapsed - @read_start_time[tid]);
      @un_read_count[pid, $fd] = count();
    } else {
      @read_bytes[$fname] = sum(args->ret);
      @read_time[$fname] = sum(elapsed - @read_start_time[tid]);
      @read_count[$fname] = count();
    }
  }

  delete(@read_fd[tid]);
  delete(@read_req_count[tid]);
  delete(@read_start_time[tid]);
}

/* write enter + exit */
tracepoint:syscalls:sys_enter_write
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER write fd=%d fname=%s count=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->fd, $fname, args->count);
  }

  @write_fd[tid] = args->fd;
  @write_req_count[tid] = args->count;
  @write_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_write
/ @tracked[pid] /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT write fd=%d fname=%s ret=%d req=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fd, $fname, args->ret, @write_req_count[tid]);
  }

  if (args->ret > 0) {
    if ($fname == "") {
      @un_write_bytes[pid, $fd] = sum(args->ret);
      @un_write_time[pid, $fd] = sum(elapsed - @write_start_time[tid]);
      @un_write_count[pid, $fd] = count();
    } else {
      @write_bytes[$fname] = sum(args->ret);
      @write_time[$fname] = sum(elapsed - @write_start_time[tid]);
      @write_count[$fname] = count();
    }
  }

  delete(@write_fd[tid]);
  delete(@write_req_count[tid]);
  delete(@write_start_time[tid]);
}

/* pread64 enter + exit */
tracepoint:syscalls:sys_enter_pread64
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER pread64 fd=%d fname=%s count=%d pos=0x%08lx\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->fd, $fname, args->count, args->pos);
  }

  @read_fd[tid] = args->fd;
  @read_req_count[tid] = args->count;
  @read_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_pread64
/ @tracked[pid] /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT pread64 fd=%d fname=%s ret=%d req=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fd, $fname, args->ret, @read_req_count[tid]);
  }

  if (args->ret > 0) {
    if ($fname == "") {
      @un_read_bytes[pid, $fd] = sum(args->ret);
      @un_read_time[pid, $fd] = sum(elapsed - @read_start_time[tid]);
      @un_read_count[pid, $fd] = count();
    } else {
      @read_bytes[$fname] = sum(args->ret);
      @read_time[$fname] = sum(elapsed - @read_start_time[tid]);
      @read_count[$fname] = count();
    }
  }

  delete(@read_fd[tid]);
  delete(@read_req_count[tid]);
  delete(@read_start_time[tid]);
}

/* pwrite64 enter + exit */
tracepoint:syscalls:sys_enter_pwrite64
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER pwrite64 fd=%d fname=%s count=%d pos=0x%08lx\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->fd, $fname, args->count, args->pos);
  }

  @write_fd[tid] = args->fd;
  @write_req_count[tid] = args->count;
  @write_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_pwrite64
/ @tracked[pid] /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT pwrite64 fd=%d fname=%s ret=%d req=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fd, $fname, args->ret, @write_req_count[tid]);
  }

  if (args->ret > 0) {
    if ($fname == "") {
      @un_write_bytes[pid, $fd] = sum(args->ret);
      @un_write_time[pid, $fd] = sum(elapsed - @write_start_time[tid]);
      @un_write_count[pid, $fd] = count();
    } else {
      @write_bytes[$fname] = sum(args->ret);
      @write_time[$fname] = sum(elapsed - @write_start_time[tid]);
      @write_count[$fname] = count();
    }
  }

  delete(@write_fd[tid]);
  delete(@write_req_count[tid]);
  delete(@write_start_time[tid]);
}

/* readv enter + exit */
tracepoint:syscalls:sys_enter_readv
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER readv fd=%d fname=%s vlen=0x%08lx\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->fd, $fname, args->vlen);
  }

  @read_fd[tid] = args->fd;
  @read_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_readv
/ @tracked[pid] /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT readv fd=%d fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fd, $fname, args->ret);
  }

  if (args->ret > 0) {
    if ($fname == "") {
      @un_read_bytes[pid, $fd] = sum(args->ret);
      @un_read_time[pid, $fd] = sum(elapsed - @read_start_time[tid]);
      @un_read_count[pid, $fd] = count();
    } else {
      @read_bytes[$fname] = sum(args->ret);
      @read_time[$fname] = sum(elapsed - @read_start_time[tid]);
      @read_count[$fname] = count();
    }
  }

  delete(@read_fd[tid]);
  delete(@read_start_time[tid]);
}

/* writev enter + exit */
tracepoint:syscalls:sys_enter_writev
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER writev fd=%d fname=%s vlen=0x%08lx\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->fd, $fname, args->vlen);
  }

  @write_fd[tid] = args->fd;
  @write_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_writev
/ @tracked[pid] /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT writev fd=%d fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fd, $fname, args->ret);
  }

  if (args->ret > 0) {
    if ($fname == "") {
      @un_write_bytes[pid, $fd] = sum(args->ret);
      @un_write_time[pid, $fd] = sum(elapsed - @write_start_time[tid]);
      @un_write_count[pid, $fd] = count();
    } else {
      @write_bytes[$fname] = sum(args->ret);
      @write_time[$fname] = sum(elapsed - @write_start_time[tid]);
      @write_count[$fname] = count();
    }
  }

  delete(@write_fd[tid]);
  delete(@write_start_time[tid]);
}

/* preadv enter + exit */
tracepoint:syscalls:sys_enter_preadv
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER preadv fd=%d fname=%s vlen=0x%08lx pos_l=0x%08lx pos_h=0x%08lx\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->fd, $fname, args->vlen, args->pos_l, args->pos_h);
  }

  @read_fd[tid] = args->fd;
  @read_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_preadv
/ @tracked[pid] /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT preadv fd=%d fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fd, $fname, args->ret);
  }

  if (args->ret > 0) {
    if ($fname == "") {
      @un_read_bytes[pid, $fd] = sum(args->ret);
      @un_read_time[pid, $fd] = sum(elapsed - @read_start_time[tid]);
      @un_read_count[pid, $fd] = count();
    } else {
      @read_bytes[$fname] = sum(args->ret);
      @read_time[$fname] = sum(elapsed - @read_start_time[tid]);
      @read_count[$fname] = count();
    }
  }

  delete(@read_fd[tid]);
  delete(@read_start_time[tid]);
}

/* pwritev enter + exit */
tracepoint:syscalls:sys_enter_pwritev
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER pwritev fd=%d fname=%s vlen=0x%08lx pos_l=0x%08lx pos_h=0x%08lx\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->fd, $fname, args->vlen, args->pos_l, args->pos_h);
  }

  @write_fd[tid] = args->fd;
  @write_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_pwritev
/ @tracked[pid] /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT pwritev fd=%d fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fd, $fname, args->ret);
  }

  if (args->ret > 0) {
    if ($fname == "") {
      @un_write_bytes[pid, $fd] = sum(args->ret);
      @un_write_time[pid, $fd] = sum(elapsed - @write_start_time[tid]);
      @un_write_count[pid, $fd] = count();
    } else {
      @write_bytes[$fname] = sum(args->ret);
      @write_time[$fname] = sum(elapsed - @write_start_time[tid]);
      @write_count[$fname] = count();
    }
  }

  delete(@write_fd[tid]);
  delete(@write_start_time[tid]);
}

/* mmap enter + exit */
tracepoint:syscalls:sys_enter_mmap
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  // store mmap inputs for exit
  @mmap_fd[tid] = args->fd;
  @mmap_len[tid] = args->len;

  if (@debug == 1) {
    printf("[%s] %d %s ENTER mmap fd=%d fname=%s addr=0x%lx len=%lu prot=0x%x flags=0x%x off: 0x%081x\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->fd, $fname, args->addr, args->len, args->prot, args->flags, args->off);
  }

  @mmap_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_mmap
/ @tracked[pid] /
{
  $fd   = @mmap_fd[tid];
  $len  = @mmap_len[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT mmap fd=%d fname=%s ret=0x%lx len=%lu\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fd, $fname, args->ret, $len);
  }

  if (args->ret != -1) {
    if ($fname == "") {
      @un_mmap_bytes[pid, $fd] = sum(args->ret);
      @un_mmap_time[pid, $fd] = sum(elapsed - @mmap_start_time[tid]);
      @un_mmap_count[pid, $fd] = count();
    } else {
      @mmap_bytes[pid, $fd] = sum(args->ret);
      @mmap_time[pid, $fd] = sum(elapsed - @mmap_start_time[tid]);
      @mmap_count[pid, $fd] = count();
    }
  }

  delete(@mmap_fd[tid]);
  delete(@mmap_len[tid]);
  delete(@mmap_start_time[tid]);
}

/* close enter + exit */
tracepoint:syscalls:sys_enter_close
/ @tracked[pid] /
{
  $fname = @fname[pid, args->fd];

  if (@debug == 1) {
    printf("[%s] %d %s ENTER close fd=%d fname=%s\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, args->fd, $fname);
  }

  @close_fd[tid] = args->fd;
}

tracepoint:syscalls:sys_exit_close
/ @tracked[pid] /
{
  $fd = @close_fd[tid];
  $fname = @fname[pid, $fd];

  if (@debug == 1) {
    printf("[%s] %d %s EXIT close fd=%d fname=%s ret=%d\n",
      strftime("%Y-%m-%d %H:%M:%S", nsecs),
      pid, comm, $fd, $fname, args->ret);
  }

  delete(@close_fd[tid]);
  delete(@fname[pid, $fd]);
}

/* ----- Intervals ----- */
/* every 60 secunds print the maps */
interval:s:60
{
  // only when not in debugging mode
  if (@debug == 0) {
    printf("--- roatating ---\n");

    print(@read_bytes);
    print(@write_bytes);

    print(@read_time);
    print(@write_time);

    print(@read_count);
    print(@write_count);

    printf("--- roatated ---\n");
  }
}
