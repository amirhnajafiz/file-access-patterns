#!/usr/bin/env bpftrace
// file: src/bpftrace/cgroup_comm_trace.bt
// usage: sudo bpftrace -o /tmp/tracing.out cgroup_comm_trace.bt <CGROUP_ID> <NAME>
// log format: [timestamp] { pid=[pid] tid=[tid] proc=[command] } { [EN|EX] [operand] } { [key=value] }

BEGIN
{
  @tracked_cgid = (uint64)$1;
  @tracked_comm = str($2);
  printf("%s START tracing file-access events for CGROUP ID %llu on %s\n", strftime("%Y-%m-%d %H:%M:%S", nsecs), $1, str($2));
}

// ----- Metadata Extraction Syscalls -----
/* creat enter + exit */
tracepoint:syscalls:sys_enter_creat
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  printf("%llu { pid=%d tid=%d proc=%s } { EN creat } { fname=%s mode=0%o }\n", nsecs, pid, tid, comm, str(args->pathname), args->mode);
  @open_fname[tid] = args->pathname;
}

tracepoint:syscalls:sys_exit_creat
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = str(@open_fname[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EX creat } { fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@open_fname[tid]);
}

/* open enter + exit */
tracepoint:syscalls:sys_enter_open
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  printf("%llu { pid=%d tid=%d proc=%s } { EN open } { fname=%s mode=0%o }\n", nsecs, pid, tid, comm, str(args->filename), args->mode);
  @open_fname[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_open
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = str(@open_fname[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EX open } { fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@open_fname[tid]);
}

/* openat enter + exit */
tracepoint:syscalls:sys_enter_openat
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  printf("%llu { pid=%d tid=%d proc=%s } { EN openat } { fname=%s mode=0%o }\n", nsecs, pid, tid, comm, str(args->filename), args->mode);
  @open_fname[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_openat
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = str(@open_fname[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EX openat } { fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@open_fname[tid]);
}

/* dup enter + exit */
tracepoint:syscalls:sys_enter_dup
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fildes];
  printf("%llu { pid=%d tid=%d proc=%s } { EN dup } { fname=%s }\n", nsecs, pid, tid, comm, $fname);
  @dup_fd[tid] = args->fildes;
}

tracepoint:syscalls:sys_exit_dup
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];
  printf("%llu { pid=%d tid=%d proc=%s } { EX dup } { fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@dup_fd[tid]);
}

/* dup2 enter + exit */
tracepoint:syscalls:sys_enter_dup2
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->oldfd];
  printf("%llu { pid=%d tid=%d proc=%s } { EN dup2 } { fname=%s }\n", nsecs, pid, tid, comm, $fname);
  @dup_fd[tid] = args->oldfd;
}

tracepoint:syscalls:sys_exit_dup2
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];
  printf("%llu { pid=%d tid=%d proc=%s } { EX dup2 } { fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fname, args->ret);
  
  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@dup_fd[tid]);
}

/* dup3 enter + exit */
tracepoint:syscalls:sys_enter_dup3
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->oldfd];
  printf("%llu { pid=%d tid=%d proc=%s } { EN dup3 } { fname=%s }\n", nsecs, pid, tid, comm, $fname);
  @dup_fd[tid] = args->oldfd;
}

tracepoint:syscalls:sys_exit_dup3
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];
  printf("%llu { pid=%d tid=%d proc=%s } { EX dup3 } { fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fname, args->ret);
  
  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@dup_fd[tid]);
}

/* statfs enter + exit */
tracepoint:syscalls:sys_enter_statfs
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  printf("%llu { pid=%d tid=%d proc=%s } { EN statfs } { fname=%s }\n", nsecs, pid, tid, comm, str(args->pathname));
  @stats_path[tid] = args->pathname;
}

tracepoint:syscalls:sys_exit_statfs
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = str(@stats_path[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EX statfs } { fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

/* statx enter + exit */
tracepoint:syscalls:sys_enter_statx
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  printf("%llu { pid=%d tid=%d proc=%s } { EN statx } { fname=%s }\n", nsecs, pid, tid, comm, str(args->filename));
  @stats_path[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_statx
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = str(@stats_path[tid]);
  printf("%llu { pid=%d tid=%d proc=%s } { EX statx } { fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

/* newstat enter + exit */
tracepoint:syscalls:sys_enter_newstat
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  printf("%llu { pid=%d tid=%d proc=%s } { EN newstat } { fname=%s }\n", nsecs, pid, tid, comm, str(args->filename));
  @stats_path[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_newstat
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = str(@stats_path[tid]);

  printf("%llu { pid=%d tid=%d proc=%s } { EX newstat } { fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

/* newlstat enter + exit */
tracepoint:syscalls:sys_enter_newlstat
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  printf("%llu { pid=%d tid=%d proc=%s } { EN newlstat } { fname=%s }\n", nsecs, pid, tid, comm, str(args->filename));
  @stats_path[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_newlstat
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = str(@stats_path[tid]);
  
  printf("%llu { pid=%d tid=%d proc=%s } { EX newlstat } { fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fname, args->ret);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@stats_path[tid]);
}

// ----- I/O Operation Syscalls -----
/* read enter + exit */
tracepoint:syscalls:sys_enter_read
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fd];

  @read_fd[tid] = args->fd;
  @read_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { EN read } { fd=%d fname=%s count=%d }\n", nsecs, pid, tid, comm, args->fd, $fname, args->count);
}

tracepoint:syscalls:sys_exit_read
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @read_start_time[tid];

  if (args->ret >= 0) {
    printf("%llu { pid=%d tid=%d proc=%s } { EX read } { fd=%d fname=%s ret=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);
  }

  delete(@read_fd[tid]);
  delete(@read_start_time[tid]);
}

/* write enter + exit */
tracepoint:syscalls:sys_enter_write
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fd];

  @write_fd[tid] = args->fd;
  @write_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { EN write } { fd=%d fname=%s count=%d }\n", nsecs, pid, tid, comm, args->fd, $fname, args->count);
}

tracepoint:syscalls:sys_exit_write
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @write_start_time[tid];

  if (args->ret >= 0) {
    printf("%llu { pid=%d tid=%d proc=%s } { EX write } { fd=%d fname=%s ret=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);
  }

  delete(@write_fd[tid]);
  delete(@write_start_time[tid]);
}

/* pread64 enter + exit */
tracepoint:syscalls:sys_enter_pread64
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fd];

  @read_fd[tid] = args->fd;
  @read_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { EN pread64 } { fd=%d fname=%s count=%d }\n", nsecs, pid, tid, comm, args->fd, $fname, args->count);
}

tracepoint:syscalls:sys_exit_pread64
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @read_start_time[tid];

  if (args->ret >= 0) {
    printf("%llu { pid=%d tid=%d proc=%s } { EX pread64 } { fd=%d fname=%s ret=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);
  }

  delete(@read_fd[tid]);
  delete(@read_start_time[tid]);
}

/* pwrite64 enter + exit */
tracepoint:syscalls:sys_enter_pwrite64
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fd];

  @write_fd[tid] = args->fd;
  @write_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { EN pwrite64 } { fd=%d fname=%s count=%d }\n", nsecs, pid, tid, comm, args->fd, $fname, args->count);
}

tracepoint:syscalls:sys_exit_pwrite64
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @write_start_time[tid];

  if (args->ret >= 0) {
    printf("%llu { pid=%d tid=%d proc=%s } { EX pwrite64 } { fd=%d fname=%s ret=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);
  }

  delete(@write_fd[tid]);
  delete(@write_start_time[tid]);
}

/* readv enter + exit */
tracepoint:syscalls:sys_enter_readv
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fd];

  @read_fd[tid] = args->fd;
  @read_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { EN readv } { fd=%d fname=%s }\n", nsecs, pid, tid, comm, args->fd, $fname);
}

tracepoint:syscalls:sys_exit_readv
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @read_start_time[tid];

  if (args->ret >= 0) {
    printf("%llu { pid=%d tid=%d proc=%s } { EX readv } { fd=%d fname=%s ret=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);
  }

  delete(@read_fd[tid]);
  delete(@read_start_time[tid]);
}

/* writev enter + exit */
tracepoint:syscalls:sys_enter_writev
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fd];

  @write_fd[tid] = args->fd;
  @write_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { EN writev } { fd=%d fname=%s }\n", nsecs, pid, tid, comm, args->fd, $fname);
}

tracepoint:syscalls:sys_exit_writev
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @write_start_time[tid];

  if (args->ret >= 0) {
    printf("%llu { pid=%d tid=%d proc=%s } { EX writev } { fd=%d fname=%s ret=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);
  }

  delete(@write_fd[tid]);
  delete(@write_start_time[tid]);
}

/* preadv enter + exit */
tracepoint:syscalls:sys_enter_preadv
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fd];

  @read_fd[tid] = args->fd;
  @read_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { EN preadv } { fd=%d fname=%s }\n", nsecs, pid, tid, comm, args->fd, $fname);
}

tracepoint:syscalls:sys_exit_preadv
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @read_start_time[tid];

  if (args->ret >= 0) {
    printf("%llu { pid=%d tid=%d proc=%s } { EX preadv } { fd=%d fname=%s ret=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);
  }

  delete(@read_fd[tid]);
  delete(@read_start_time[tid]);
}

/* pwritev enter + exit */
tracepoint:syscalls:sys_enter_pwritev
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fd];

  @write_fd[tid] = args->fd;
  @write_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { EN pwritev } { fd=%d fname=%s }\n", nsecs, pid, tid, comm, args->fd, $fname);
}

tracepoint:syscalls:sys_exit_pwritev
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];
  $latency = elapsed - @write_start_time[tid];

  if (args->ret >= 0) {
    printf("%llu { pid=%d tid=%d proc=%s } { EX pwritev } { fd=%d fname=%s ret=%d latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $latency);
  }

  delete(@write_fd[tid]);
  delete(@write_start_time[tid]);
}

/* mmap enter + exit */
tracepoint:syscalls:sys_enter_mmap
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fd];

  // store mmap inputs for exit
  @mmap_fd[tid] = args->fd;
  @mmap_len[tid] = args->len;
  @mmap_start_time[tid] = elapsed;

  printf("%llu { pid=%d tid=%d proc=%s } { EN mmap } { fd=%d fname=%s addr=%lu len=%lu prot=0x%x flags=0x%x off: 0x%081x }\n", nsecs, pid, tid, comm, args->fd, $fname, args->addr, args->len, args->prot, args->flags, args->off);
}

tracepoint:syscalls:sys_exit_mmap
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd   = @mmap_fd[tid]; 
  $fname = @fname[pid, $fd];
  $len  = @mmap_len[tid];
  $latency = elapsed - @mmap_start_time[tid];

  if (args->ret >= 0) {
    printf("%llu { pid=%d tid=%d proc=%s } { EX mmap } { fd=%d fname=%s ret=%lu len=%llu latency=%llu }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret, $len, $latency);
  }

  delete(@mmap_fd[tid]);
  delete(@mmap_len[tid]);
  delete(@mmap_start_time[tid]);
}

/* vfs read + write */
/*
kfunc:vfs_read 
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  printf("%llu { pid=%d tid=%d proc=%s } { EN vfs_read } { file=%s }\n", nsecs, pid, tid, comm, str(args->file->f_path.dentry->d_name.name));
}

kfunc:vfs_write
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  printf("%llu { pid=%d tid=%d proc=%s } { EN vfs_write } { file=%s }\n", nsecs, pid, tid, comm, str(args->file->f_path.dentry->d_name.name));
}
*/

/* page fault user */
tracepoint:exceptions:page_fault_user
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  printf("%llu { pid=%d tid=%d proc=%s } { EN page_fault_user } { addr=%lu }\n", nsecs, pid, tid, comm, args->address); 
}

/* close enter + exit */
tracepoint:syscalls:sys_enter_close
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fname = @fname[pid, args->fd];
  printf("%llu { pid=%d tid=%d proc=%s } { EN close } { fd=%d fname=%s }\n", nsecs, pid, tid, comm, args->fd, $fname);
  @close_fd[tid] = args->fd;
}

tracepoint:syscalls:sys_exit_close
/ cgroup == @tracked_cgid && comm == @tracked_comm /
{
  $fd = @close_fd[tid];
  $fname = @fname[pid, $fd];

  if (args->ret >= 0) {
    printf("%llu { pid=%d tid=%d proc=%s } { EX close } { fd=%d fname=%s ret=%d }\n", nsecs, pid, tid, comm, $fd, $fname, args->ret);
  }
  
  delete(@close_fd[tid]);
}