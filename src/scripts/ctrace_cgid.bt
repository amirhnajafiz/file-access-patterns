#!/usr/bin/env bpftrace
// file: ctrace_cgid.bt
// usage: sudo bpftrace -o /tmp/tracing.out ctrace_cgid.bt <CGROUP_ID>

BEGIN
{
  @tracked_cgid = (uint64)$1;
  printf("[%s] START tracing file-access events for CGROUP ID %llu\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs), $1);
}

/* fork: child inherits tracking if parent is in target cgroup */
tracepoint:sched:sched_process_fork
/ cgroup == @tracked_cgid /
{
  printf("[%s] %d %s FORK -> %d %s [CGID %llu]\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs),
         args->parent_pid, args->parent_comm, args->child_pid, args->child_comm, cgroup);
}

/* exec: just log exec events from tracked cgroup */
tracepoint:sched:sched_process_exec
/ cgroup == @tracked_cgid /
{
  printf("[%s] %d %s EXEC %d %s [CGID %llu]\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs),
         args->old_pid, comm, args->pid, str(args->filename), cgroup);
}

/* openat enter + exit */
tracepoint:syscalls:sys_enter_openat
/ cgroup == @tracked_cgid /
{
  printf("[%s] %d %s ENTER openat dfd=%d fname=%s flags=0x%x mode=0%o [CGID %llu]\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs),
         pid, comm, args->dfd, str(args->filename), args->flags, args->mode, cgroup);

  @open_fname[tid] = args->filename;
}

tracepoint:syscalls:sys_exit_openat
/ cgroup == @tracked_cgid /
{
  $fname = str(@open_fname[tid]);

  printf("[%s] %d %s EXIT openat fname=%s ret=%d [CGID %llu]\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs),
         pid, comm, $fname, args->ret, cgroup);

  if (args->ret >= 0) {
    @fname[pid, args->ret] = $fname;
    @read_bytes[$fname] = sum(0);
    @write_bytes[$fname] = sum(0);
    @read_time[$fname] = sum(0);
    @write_time[$fname] = sum(0);
  }

  delete(@open_fname[tid]);
}

/* dup enter + exit, update fname map in case of dup (cgroup-filtered) */
tracepoint:syscalls:sys_enter_dup
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fildes];
  printf("[%s] %d %s ENTER dup fildes=%d fname=%s\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm, args->fildes, $fname);
  @dup_fd[tid] = args->fildes;
}

tracepoint:syscalls:sys_exit_dup
/ cgroup == @tracked_cgid /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];

  printf("[%s] %d %s EXIT dup fd=%d fname=%s ret=%d\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm, $fd, $fname, args->ret);

  if (args->ret != -1) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@fname[pid, $fd]);
  delete(@dup_fd[tid]);
}

/* dup2 enter + exit, update fname map in case of dup2 (cgroup-filtered) */
tracepoint:syscalls:sys_enter_dup2
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->oldfd];
  printf("[%s] %d %s ENTER dup2 oldfd=%d newfd=%d fname=%s\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm,
         args->oldfd, args->newfd, $fname);
  @dup_fd[tid] = args->oldfd;
}

tracepoint:syscalls:sys_exit_dup2
/ cgroup == @tracked_cgid /
{
  $fd = @dup_fd[tid];
  $fname = @fname[pid, $fd];

  printf("[%s] %d %s EXIT dup2 fd=%d fname=%s ret=%d\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm, $fd, $fname, args->ret);
  
  if (args->ret != -1) {
    @fname[pid, args->ret] = $fname;
  }

  delete(@fname[pid, $fd]);
  delete(@dup_fd[tid]);
}

/* newfstat enter + exit, count total number of fstats (cgroup-filtered) */
tracepoint:syscalls:sys_enter_newfstat
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];
  printf("[%s] %d %s ENTER newfstat fd=%d fname=%s\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm, args->fd, $fname);
  @fstat_fd[tid] = args->fd;
}

tracepoint:syscalls:sys_exit_newfstat
/ cgroup == @tracked_cgid /
{
  $fd = @fstat_fd[tid];
  $fname = @fname[pid, $fd];

  printf("[%s] %d %s EXIT newfstat fd=%d fname=%s ret=%d\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm, $fd, $fname, args->ret);
  
  if (args->ret != -1) {
    @num_fstat[$fname] = count();
  }

  delete(@fstat_fd[tid]);
}

/* read enter + exit */
tracepoint:syscalls:sys_enter_read
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];
  printf("[%s] %d %s ENTER read fd=%d fname=%s count=%d [CGID %llu]\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs),
         pid, comm, args->fd, $fname, args->count, cgroup);

  @read_fd[tid] = args->fd;
  @read_count[tid] = args->count;
  @read_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_read
/ cgroup == @tracked_cgid /
{
  $fd = @read_fd[tid];
  $fname = @fname[pid, $fd];

  printf("[%s] %d %s EXIT read fd=%d fname=%s ret=%d req=%d [CGID %llu]\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs),
         pid, comm, $fd, $fname, args->ret, @read_count[tid], cgroup);

  if (args->ret > 0) {
    @read_bytes[$fname] = sum(args->ret);
    @read_time[$fname] = sum(elapsed - @read_start_time[tid]);
  }

  delete(@read_fd[tid]);
  delete(@read_count[tid]);
  delete(@read_start_time[tid]);
}

/* write enter + exit */
tracepoint:syscalls:sys_enter_write
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];
  printf("[%s] %d %s ENTER write fd=%d fname=%s count=%d [CGID %llu]\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs),
         pid, comm, args->fd, $fname, args->count, cgroup);

  @write_fd[tid] = args->fd;
  @write_count[tid] = args->count;
  @write_start_time[tid] = elapsed;
}

tracepoint:syscalls:sys_exit_write
/ cgroup == @tracked_cgid /
{
  $fd = @write_fd[tid];
  $fname = @fname[pid, $fd];

  printf("[%s] %d %s EXIT write fd=%d fname=%s ret=%d req=%d [CGID %llu]\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs),
         pid, comm, $fd, $fname, args->ret, @write_count[tid], cgroup);

  if (args->ret > 0) {
    @write_bytes[$fname] = sum(args->ret);
    @write_time[$fname] = sum(elapsed - @write_start_time[tid]);
  }

  delete(@write_fd[tid]);
  delete(@write_count[tid]);
  delete(@write_start_time[tid]);
}

/* close enter + exit */
tracepoint:syscalls:sys_enter_close
/ cgroup == @tracked_cgid /
{
  $fname = @fname[pid, args->fd];
  printf("[%s] %d %s ENTER close fd=%d fname=%s [CGID %llu]\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs),
         pid, comm, args->fd, $fname, cgroup);

  @close_fd[tid] = args->fd;
}

tracepoint:syscalls:sys_exit_close
/ cgroup == @tracked_cgid /
{
  $fd = @close_fd[tid];
  $fname = @fname[pid, $fd];

  printf("[%s] %d %s EXIT close fd=%d fname=%s ret=%d [CGID %llu]\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs),
         pid, comm, $fd, $fname, args->ret, cgroup);

  delete(@close_fd[tid]);
  delete(@fname[pid, $fd]);
}

/* process exit within cgroup */
tracepoint:sched:sched_process_exit
/ cgroup == @tracked_cgid /
{
  printf("[%s] %d %s EXIT_PROCESS [CGID %llu]\n",
         strftime("%Y-%m-%d %H:%M:%S", nsecs), pid, comm, cgroup);

  delete(@fname[pid, 0]);
  delete(@fname[pid, 1]);
  delete(@fname[pid, 2]);
}
